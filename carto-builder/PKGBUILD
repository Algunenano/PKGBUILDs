# Maintainer: Javier Torres <javier@javiertorres.eu>
pkgname=carto-builder
pkgver=4.37.0.r385.1fbb198983.bring_profiler_to_2020
pkgrel=1
pkgdesc="Location Intelligence & Data Visualization tool"
arch=('x86_64')
url="https://github.com/CartoDB/cartodb"
license=('BSD')
groups=()
depends=('ruby-bundler' 'postgis' 'redis' 'python' 'unp' 'python-gdal')
makedepends=('git' 'npm' 'nodejs')
provides=()
conflicts=()
replaces=()
backup=('etc/carto/builder/config/app_config.yml' 'etc/carto/builder/config/database.yml')
options=()
_branch='bring-profiler-to-2020'
source=("git+https://github.com/CartoDB/cartodb.git#branch=$_branch"
#source=('git+https://github.com/Algunenano/cartodb.git#branch=update_sequel'
        'default_config.patch'
        'carto-builder.service'
        'carto-resque.service'
        'resque_verbose.patch')
noextract=()
sha256sums=('SKIP'
            '94211ab0adf0eec0d7a62410df0b038af72b9e881d438eb384a48ce9d3f377e0'
            '5bd32b6e3f9c29683cfa9922fe0bb15f0c98bd8735a31ca46ad36c1d51c6818a'
            'aa0164ffda931984000aa527c452a24a95fe4f31e1674a7f68a8fa82e642ccee'
            'c7eb3357eaf87fdca0c9e57d608704cbbe3302fa5caa73c384ebed067dfcaac6')

pkgver() {
  cd "$srcdir/cartodb"
  printf "%s.%s" "$(git describe --long | cut -c2- | sed 's/\([^-]*-\)g/r\1/;s/-/./g')" "$(echo $_branch | sed 's/-/_/g')"
}

prepare() {
  cd "$srcdir/cartodb"
  patch -p1 < "$srcdir/default_config.patch"
  patch -p1 < "$srcdir/resque_verbose.patch"
}

build() {
  cd "$srcdir/cartodb"
  bundle install --path vendor/bundle
  npm ci && npm run && npm run carto-node && npm run build:static && npm run build:dev
  git submodule update --init
  bundle exec node_modules/.bin/grunt --no-node-checker
}

package() {
  cd "$srcdir/cartodb"
  mkdir -p "$pkgdir/opt/carto/builder"
  cp -ar app gears .bundle config config.ru db Gemfile Gemfile.lock lib package.json public Rakefile script services vendor "$pkgdir/opt/carto/builder"
  rm "$pkgdir/opt/carto/builder/lib/tasks/random_data.rake"
  find "$pkgdir/opt/carto/builder" -name spec -type d -prune -exec rm -r {} \;

  # Link to config
  rm -f "$pkgdir/opt/carto/builder/config/app_config.yml" "$pkgdir/opt/carto/builder/config/database.yml"
  ln -s "/etc/carto/builder/config/app_config.yml" "/etc/carto/builder/config/database.yml" "$pkgdir/opt/carto/builder/config"

  install -Dm0644 "$srcdir/carto-builder.service" "$pkgdir/usr/lib/systemd/system/carto-builder.service"
  install -Dm0644 "$srcdir/carto-resque.service" "$pkgdir/usr/lib/systemd/system/carto-resque.service"
  install -Dm0644 config/app_config.yml.sample "$pkgdir/etc/carto/builder/config/app_config.yml"
  install -Dm0644 config/database.yml.sample "$pkgdir/etc/carto/builder/config/database.yml"
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
