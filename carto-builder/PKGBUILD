# Maintainer: Javier Torres <javier@javiertorres.eu>
pkgname=carto-builder
pkgver=4.31.0.r714.4cdd37ae50
pkgrel=1
pkgdesc="Location Intelligence & Data Visualization tool"
arch=('x86_64')
url="https://github.com/CartoDB/cartodb"
license=('BSD')
groups=()
depends=('ruby2.5-bundler<2.0.0' 'postgis' 'redis' 'python' 'unp' 'python-gdal')
makedepends=('git' 'npm' 'nodejs')
provides=()
conflicts=()
replaces=()
backup=('etc/carto/builder/config/app_config.yml' 'etc/carto/builder/config/database.yml')
options=()
source=('git+https://github.com/CartoDB/cartodb.git#branch=poltergeist_pin'
#source=('git+https://github.com/Algunenano/cartodb.git#branch=update_sequel'
        'default_config.patch'
        'carto-builder.service'
        'carto-resque.service'
        'resque_verbose.patch')
noextract=()
sha256sums=('SKIP'
            '4182e4747c6e9954299eee5405df8584f4d7d07d84379a36c019c8a344a807ee'
            '56a27229484b983327dd2f5c132c2960a9d42a902d2c92fb5e04db808ae72091'
            '1699b0d5c8d5ce09121f6deb304a8207d1bb908ba95b8b57b6a0f0d097b1eb80'
            'c7eb3357eaf87fdca0c9e57d608704cbbe3302fa5caa73c384ebed067dfcaac6')

pkgver() {
  cd "$srcdir/cartodb"
  printf "%s" "$(git describe --long | cut -c2- | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

prepare() {
  cd "$srcdir/cartodb"
  patch -p1 < "$srcdir/default_config.patch"
  patch -p1 < "$srcdir/resque_verbose.patch"
}

build() {
  cd "$srcdir/cartodb"
  rm Gemfile.lock && bundle-2.5 install --path vendor/bundle
  npm ci && npm run && npm run carto-node && npm run build:static && npm run build:dev
  git submodule update --init
  bundle-2.5 exec node_modules/.bin/grunt --no-node-checker
}

package() {
  cd "$srcdir/cartodb"
  mkdir -p "$pkgdir/opt/carto/builder"
  cp -ar app gears .bundle config config.ru db Gemfile Gemfile.lock lib package.json public Rakefile script services vendor "$pkgdir/opt/carto/builder"
  rm "$pkgdir/opt/carto/builder/lib/tasks/random_data.rake"
  find "$pkgdir/opt/carto/builder" -name spec -type d -prune -exec rm -r {} \;

  # Link to config
  rm -f "$pkgdir/opt/carto/builder/config/app_config.yml" "$pkgdir/opt/carto/builder/config/database.yml"
  ln -s "/etc/carto/builder/config/app_config.yml" "/etc/carto/builder/config/database.yml" "$pkgdir/opt/carto/builder/config"

  install -Dm0644 "$srcdir/carto-builder.service" "$pkgdir/usr/lib/systemd/system/carto-builder.service"
  install -Dm0644 "$srcdir/carto-resque.service" "$pkgdir/usr/lib/systemd/system/carto-resque.service"
  install -Dm0644 config/app_config.yml.sample "$pkgdir/etc/carto/builder/config/app_config.yml"
  install -Dm0644 config/database.yml.sample "$pkgdir/etc/carto/builder/config/database.yml"
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
