_pkgname=postgis
pkgname=$_pkgname-git
pkgver=3.0_svn_trunk.12487.e90f4dcfd
pkgrel=1
pkgdesc="Adds support for geographic objects to PostgreSQL"
arch=('i686' 'x86_64')
url="http://postgis.net/"
license=('GPL')
depends=('postgresql' 'gdal' 'json-c' 'proj' 'protobuf-c' 'sfcgal' 'libxml2')
makedepends=('git' 'clang')
provides=("${_pkgname}=${pkgver}")
conflicts=("$_pkgname")
_branch="svn-trunk"
source=("$_pkgname::git+https://github.com/postgis/postgis.git#branch=$_branch")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  printf "3.0_%s.%s.%s" "$(echo $_branch | sed 's/-/_/g')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$_pkgname"
  ./autogen.sh
  ./configure --prefix=/usr CC=clang CXX=clang++
  make
}

package() {
  cd "${_pkgname}"
  make DESTDIR="${pkgdir}" install
}
