_pkgname=postgis
pkgname=$_pkgname-git
pkgver=3.0_libwagyu_LTO.12609.a7806e460
pkgrel=1
pkgdesc="Adds support for geographic objects to PostgreSQL"
arch=('i686' 'x86_64')
url="http://postgis.net/"
license=('GPL')
depends=('postgresql' 'gdal' 'json-c' 'proj' 'protobuf-c' 'sfcgal' 'libxml2')
makedepends=('git' 'clang')
provides=("${_pkgname}=${pkgver}")
conflicts=("$_pkgname")
_branch="libwagyu"
source=("$_pkgname::git+https://github.com/Algunenano/postgis.git#branch=$_branch")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  printf "3.0_%s_LTO.%s.%s" "$(echo $_branch | sed 's/-/_/g')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$_pkgname"
  ./autogen.sh
  ./configure --with-wagyu --prefix=/usr CC=clang CXX=clang++ CFLAGS="$CFLAGS -flto=thin" CXXFLAGS="$CXXFLAGS -flto=thin" LDFLAGS="$LDFLAGS -flto=thin"
  make
}

package() {
  cd "${_pkgname}"
  make DESTDIR="${pkgdir}" install
}
