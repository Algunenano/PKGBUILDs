_pkgname=postgis
pkgname=$_pkgname-git
pkgver=3.1.0dev_master_13142.ff0845764
pkgrel=1
pkgdesc="Adds support for geographic objects to PostgreSQL"
arch=('i686' 'x86_64')
url="http://postgis.net/"
license=('GPL')
depends=('postgresql' 'gdal' 'json-c' 'proj' 'protobuf-c' 'libxml2')
optdepends=('sfcgal')
makedepends=('git' 'clang')
provides=("${_pkgname}=${pkgver}")
conflicts=("$_pkgname")
_branch="master"
source=("$_pkgname::git+https://github.com/Algunenano/postgis.git#branch=$_branch")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  source Version.config
  printf "%s.%s.%s_%s_%s.%s" "$POSTGIS_MAJOR_VERSION" "$POSTGIS_MINOR_VERSION" "$POSTGIS_MICRO_VERSION" "$(echo $_branch | sed 's/-/_/g')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$_pkgname"
  ./autogen.sh
  ./configure --prefix=/usr CC=clang CXX=clang++
  make
}

package() {
  cd "${_pkgname}"
  make DESTDIR="${pkgdir}" install
}
