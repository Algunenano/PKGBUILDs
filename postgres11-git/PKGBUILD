_pkgname=postgresql
pkgname=$_pkgname-git
pkgver=11_3.r38.57e85fa2cb
pkgrel=1
pkgdesc="Git version of the PostgreSQL database (includes both server and libs)"
arch=('i686' 'x86_64')
url="https://wiki.postgresql.org/wiki/HowToBetaTest"
license=('custom:PostgreSQL')
backup=('etc/pam.d/postgresql' 'etc/logrotate.d/postgresql')
depends=('libxml2' 'readline>=6.0' 'openssl>=1.0.0')
makedepends=('python2' 'perl' 'tcl>=8.6.0' 'clang')
optdepends=('python2: for PL/Python support'
            'perl: for PL/Perl support'
            'tcl: for PL/Tcl support')
conflicts=('postgresql' 'postgresql-libs' 'postgresql-testing' 'postgresql-client')
provides=("postgresql=$pkgver" "postgresql-libs=$pkgver" "postgresql-client=$pkgver")
_reponame="postgres"
_branch="REL_11_STABLE"
source=("git+https://github.com/Algunenano/$_reponame.git#branch=$_branch"
        postgresql-run-socket.patch
        postgresql.pam
        postgresql.logrotate
        postgresql.service
        postgresql.tmpfiles.conf)
install=postgresql.install

sha256sums=('SKIP'
            '8538619cb8bea51078b605ad64fe22abd6050373c7ae3ad6595178da52f6a7d9'
            '57dfd072fd7ef0018c6b0a798367aac1abb5979060ff3f9df22d1048bb71c0d5'
            '6abb842764bbed74ea4a269d24f1e73d1c0b1d8ecd6e2e6fb5fb10590298605e'
            '9c7ed2da7f6ce9f932852b6f798a90ad1d79c97ee148c6e98d02e514fe3f5e36'
            '7e086d70e0dcaa6ce45693b4e826ce92d770192b3aff5642319b1ef279d88dc4')

pkgver() {
  cd "$_reponame"
  git describe --tags | sed 's/\([^-]*-\)g/r\1/;s/-/./g' | sed 's/REL_//g'
}

build() {
  cd "$_reponame"

  export CC=clang
  export CXX=clang++
#   export CFLAGS="$CFLAGS -fsanitize=memory -fsanitize-memory-track-origins=2 -fsanitize-recover=memory"
  ## Enable LLVM

  patch -Np1 < ../postgresql-run-socket.patch
  ./configure --prefix=/usr \
  --mandir=/usr/share/man \
  --datadir=/usr/share/postgresql \
  --sysconfdir=/etc \
  --with-gssapi \
  --with-libxml \
  --with-openssl \
  --with-perl \
  --with-python PYTHON=/usr/bin/python2 \
  --with-tcl \
  --with-pam \
  --with-system-tzdata=/usr/share/zoneinfo \
  --with-uuid=e2fs \
  --enable-nls \
  --enable-thread-safety \
  --with-llvm # --enable-cassert --enable-debug

  make
  make -C contrib
}

package() {
  cd "$_reponame"

  # install
  make DESTDIR="${pkgdir}" install
  make -C contrib DESTDIR="${pkgdir}" install

  # install license
  install -D -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgbase}/LICENSE"

  install -D -m644 "${srcdir}/postgresql.tmpfiles.conf" "${pkgdir}/usr/lib/tmpfiles.d/postgresql.conf"
  install -D -m644 "${srcdir}/postgresql.service"       "${pkgdir}/usr/lib/systemd/system/postgresql.service"
  install -D -m644 "${srcdir}/postgresql.pam"           "${pkgdir}/etc/pam.d/postgresql"
  install -D -m644 "${srcdir}/postgresql.logrotate"     "${pkgdir}/etc/logrotate.d/postgresql"
}
