_pkgnamebase=crankshaft
_pkgname=carto-$_pkgnamebase
pkgname=${_pkgname}-git
pkgver=0.8.2.r19.g6bc963a
pkgrel=1
pkgdesc="CARTO Spatial Analysis extension for PostgreSQL."
arch=(any)
url="https://github.com/CartoDB/crankshaft"
license=(custom)
depends=(python postgresql python-joblib python-pysal python-scikit-learn)
makedepends=(python-setuptools)
conflicts=("$_pkgname")
provides=("${_pkgname}=${pkgver}")
source=("git://github.com/Algunenano/crankshaft#branch=pg12python3")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgnamebase"
  ( set -o pipefail
    git describe --long --tags 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build() {
  cd "$_pkgnamebase/src/py/$_pkgnamebase"
  python2 setup.py build
}

package() {
  #LICENSE
  cd "$_pkgnamebase"
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  #SQL functions
  install -d "$pkgdir/usr/share/postgresql/extension/"
  cd "src/pg"
  DESTDIR=$pkgdir make deploy

  #PYTHON module
  cd "../py/$_pkgnamebase"
  python setup.py install --root="${pkgdir}" || return 1
}
