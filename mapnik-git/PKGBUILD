# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: David Dent <thewinch@gmail.com>
# Contributor: orbisvicis <orbisvicis@gmail.com>

pkgname=mapnik-git
_pkgname=mapnik
pkgver=3.0.15.0.g15d18070a
pkgrel=1
pkgdesc="A powerful map rendering toolkit used by OpenStreetmap and others"
arch=('i686' 'x86_64')
url="http://mapnik.org/"
license=('LGPL')
depends=('boost-libs' 'icu' 'libpng' 'libjpeg' 'libtiff' 'freetype2'
	 'libxml2' 'python2' 'proj' 'cairo' 'cairomm' 'python2-cairo'
	 'postgresql-libs' 'postgis' 'gdal' 'curl' 'libltdl')
optdepends=('libxslt:         Web Map Service'
            'python2-lxml:    Web Map Service'
            'python2-pillow:  Web Map Service'
            'python-nose:     Web Map Service'
            'apache:          Web Map Service'
            'mod_fastcgi:     Web Map Service - or:'
            'mod_fcgid:       Web Map Service - or:'
            'mod_wsgi2:       Web Map Service')
makedepends=('scons' 'boost' 'git' 'clang')
install="mapnik.install"
conflicts=("$_pkgname")
provides=("${_pkgname}=${pkgver}")
options=(staticlibs)
source=("git://github.com/mapnik/mapnik.git#tag=v3.0.15"
        'icu59.patch')
sha256sums=('SKIP' 'b391a775f28ec9bc7d5d2a8ef429789d8c6158bdf8023f395501fbfc8137cc22')


prepare() {
  cd "$srcdir/$_pkgname"
  patch -Np1 -i ../icu59.patch
}

pkgver() {
  cd "$srcdir/$_pkgname"

  git describe --long | cut -c2- | sed 's/-/./g'
}

build() {
  cd "$srcdir/$_pkgname"
  git submodule update --init
  PYTHON=python2
  scons configure \
    PREFIX="/usr" \
    INPUT_PLUGINS=all \
    XMLPARSER=libxml2 \
    DESTDIR="$pkgdir" \
    CUSTOM_CXXFLAGS="-DBOOST_OPTIONAL_CONFIG_USE_OLD_IMPLEMENTATION_OF_OPTIONAL" \
    CXX="clang++"
  scons -j1 # $MAKEFLAGS
}

package() {
  cd "$srcdir/$_pkgname"
  scons install
}


