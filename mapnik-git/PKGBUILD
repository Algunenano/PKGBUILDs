# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: David Dent <thewinch@gmail.com>
# Contributor: orbisvicis <orbisvicis@gmail.com>

pkgname=mapnik-git
_pkgname=mapnik
pkgver=3.0.15.5.g3cc7185e1
pkgrel=1
pkgdesc="A powerful map rendering toolkit used by OpenStreetmap and others"
arch=('i686' 'x86_64')
url="http://mapnik.org/"
license=('LGPL')
depends=('boost-libs' 'icu' 'libpng' 'libjpeg' 'libtiff' 'freetype2'
	 'libxml2' 'python2' 'proj' 'cairo' 'cairomm' 'python2-cairo'
	 'postgresql-libs' 'postgis' 'gdal' 'curl' 'libltdl')
optdepends=('libxslt:         Web Map Service'
            'python2-lxml:    Web Map Service'
            'python2-pillow:  Web Map Service'
            'python2-nose:    Web Map Service'
            'apache:          Web Map Service'
            'mod_fastcgi:     Web Map Service'
            'mod_fcgid:       Web Map Service'
            'mod_wsgi2:       Web Map Service')
makedepends=('boost' 'git' 'clang')
install="mapnik.install"
conflicts=("$_pkgname")
provides=("${_pkgname}=${pkgver}")
source=("git://github.com/algunenano/mapnik.git#branch=performance_stats")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  git describe --long --tags | cut -c2- | sed 's/-/./g'
}

build() {
  cd "$_pkgname"

  git submodule update --init -- deps/mapbox/variant # deps/mapbox/geometry deps/mapbox/protozero

  python2 scons/scons.py configure \
    PREFIX="/usr" \
    DESTDIR="$pkgdir" \
    INPUT_PLUGINS=all \
    XMLPARSER=libxml2 \
    ENABLE_METRICS=True \
    CXXFLAGS="$CXXFLAGS" \
    CUSTOM_CXXFLAGS="-DBOOST_OPTIONAL_CONFIG_USE_OLD_IMPLEMENTATION_OF_OPTIONAL" \
    CXX="clang++"

  python2 scons/scons.py $MAKEFLAGS
}

package() {
  cd "$_pkgname"
  python2 scons/scons.py install
  sed -i 's/CONFIG_PREFIX=.*/CONFIG_PREFIX="\/usr"/' $pkgdir/usr/bin/mapnik-config
}
