# Maintainer: Javier Torres <jtorres@carto.com>
pkgname=carto-tiler
pkgver=6.0.0.r101.e48f75b0.master_base.mapnik.3.0.15.7.3.gf589b7213.1
pkgrel=1
pkgdesc="CARTO Maps API tiler. It extends Windshaft with some extra functionality"
arch=('x86_64')
url="https://github.com/CartoDB/Windshaft-cartodb"
license=('BSD')
depends=('mapnik' 'nodejs' 'pango' 'redis' 'gdal' 'mapnik')
makedepends=('git' 'npm' 'python2' 'boost')
backup=('etc/carto/tiler.conf')
owner='Algunenano'
reponame='Windshaft-cartodb'
branch='master_base'
source=("git+https://github.com/$owner/$reponame.git#branch=$branch"
        'configuration.patch'
        'carto-tiler.service')
sha256sums=('SKIP'
            'eecc12b87ca196bcf4baf67952078513b465cb813ea5a7043d48447e44cd942a'
            'e0a7912e260c6dfc1e0fe42fde7f01d1f2dbbab17587d1cfb43b75277745dfa7')

pkgver() {
  cd "$reponame"
  printf "%s.$branch.mapnik.%s" "$(git describe --long --tags | sed 's/\([^-]*-\)g/r\1/;s/-/./g')" "$(pacman -Q mapnik-git | awk '{print $2}' | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

prepare() {
  cd "$reponame"
  patch -p1 < "$srcdir/configuration.patch"
}

build() {
  cd "$reponame"
  rm -rf node_modules/ yarn.lock package-lock.json
  NODE_MAPNIK_BUILD=release_base npm_config_python=python2 npm install --production --build-from-source --shared_gdal
}

package() {
  cd "$reponame"
  mkdir -p "$pkgdir/opt/carto/tiler"
  cp -ar app.js assets lib node_modules package.json "$pkgdir/opt/carto/tiler"

  install -Dm0644 "$srcdir/carto-tiler.service" "$pkgdir/usr/lib/systemd/system/carto-tiler.service"
  install -Dm0644 config/environments/development.js.example "$pkgdir/etc/carto/tiler.conf"
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
