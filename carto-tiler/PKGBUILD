# Maintainer: Javier Torres <jtorres@carto.com>
pkgname=carto-tiler
pkgver=6.5.1.r21.f82a7a14.master
pkgrel=1
pkgdesc="CARTO Maps API tiler. It extends Windshaft with some extra functionality"
arch=('x86_64')
url="https://github.com/CartoDB/Windshaft-cartodb"
license=('BSD')
depends=('nodejs-lts-dubnium' 'redis' 'postgresql')
makedepends=('git' 'npm')
backup=('etc/carto/tiler.conf')
owner='CartoDB'
reponame='Windshaft-cartodb'
branch='master'
source=("git+https://github.com/$owner/$reponame.git#branch=$branch"
        'configuration.patch'
        'carto-tiler.service')
sha256sums=('SKIP'
            '9f092ee9f8eb937b49d530485fe34d67d818c6e9267709bff161eb5ceb22ef6c'
            'e0a7912e260c6dfc1e0fe42fde7f01d1f2dbbab17587d1cfb43b75277745dfa7')

pkgver() {
  cd "$reponame"
  printf "%s.%s" "$(git describe --long --tags | sed 's/\([^-]*-\)g/r\1/;s/-/./g')" "$(echo $branch | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

prepare() {
  cd "$reponame"
  patch -p1 < "$srcdir/configuration.patch"
}

build() {
  cd "$reponame"
  npm ci --production
}

package() {
  cd "$reponame"
  mkdir -p "$pkgdir/opt/carto/tiler"
  cp -ar app.js assets lib node_modules package.json "$pkgdir/opt/carto/tiler"

  install -Dm0644 "$srcdir/carto-tiler.service" "$pkgdir/usr/lib/systemd/system/carto-tiler.service"
  install -Dm0644 config/environments/development.js.example "$pkgdir/etc/carto/tiler.conf"
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
