_pkgname=odbc_fdw
pkgname=$_pkgname-git
pkgver=0.2.0.r58.gf21f92b
pkgrel=1
pkgdesc="PostgreSQL extension that implements a FDW for remote databases using ODBC"
arch=('x86_64')
url="https://github.com/CartoDB/odbc_fdw"
license=('BSD')
depends=('postgresql')
makedepends=('git')
conflicts=("$_pkgname")
provides=("${_pkgname}=${pkgver}")
source=("git://github.com/CartoDB/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
    cd "$_pkgname"
    ( set -o pipefail
        git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
        printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

build() {
    cd "$_pkgname"
    make
}

package() {
    cd "$_pkgname"
    DESTDIR=$pkgdir make install
    install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
