# $Id: $
# Maintainer: Dmitry Bilunov <kmeaw@yandex-team.ru>
pkgname=clickhouse
pkgver=1.1.54267
_pkgver_full="v$pkgver-testing"
pkgrel=1
pkgdesc='An open-source column-oriented database management system that allows generating analytical data reports in real time'
arch=('i686' 'x86_64')
url='https://clickhouse.yandex/'
license=('Apache')
depends=('ncurses' 'readline' 'unixodbc' 'boost-libs' 'termcap' 'poco' 'libmariadbclient' 'zstd' 'lz4' 'double-conversion' 're2' 'gperftools')
makedepends=('cmake' 'git' 'sparsehash')
conflicts=('clickhouse')
source=("git+https://github.com/yandex/ClickHouse.git#tag=$pkgver_full"
        clickhouse-server.service
        re2-length.patch)
sha256sums=('SKIP'
            '497b1113522a25399b891373dd434b55c6537a0417b0083ef404cff5307e8381'
            'b6ad77d9fa278c3b53803ccc11443aab5920187ea1e6bc25327758da01666c49')
backup=('etc/clickhouse-client/config.xml' 'etc/clickhouse-server/config.xml' 'etc/clickhouse-server/users.xml')
install=$pkgname.install

build() {
  cd "ClickHouse/"
  sed -e 's/mysqlxx common\(.*\) \(\${Z_LIB}\)/mysqlxx \2 common\1/' -i libs/libmysqlxx/CMakeLists.txt

  patch -p1 < ../re2-length.patch

        # Native dependencies that are not ready:
        #-D USE_INTERNAL_UNWIND_LIBRARY:BOOL=False \
        #-D USE_INTERNAL_ZOOKEEPER_LIBRARY:BOOL=False \
        #-D USE_INTERNAL_CCTZ_LIBRARY:BOOL=False
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr \
        -D CMAKE_BUILD_TYPE:STRING=Release \
        -D USE_STATIC_LIBRARIES:BOOL=False \
        -D USE_INTERNAL_POCO_LIBRARY:BOOL=False \
        -D USE_INTERNAL_BOOST_LIBRARY:BOOL=False \
        -D USE_INTERNAL_ZLIB_LIBRARY:BOOL=False \
        -D USE_INTERNAL_ZSTD_LIBRARY:BOOL=False \
        -D USE_INTERNAL_LZ4_LIBRARY:BOOL=False \
        -D USE_INTERNAL_SPARCEHASH_LIBRARY:BOOL=False \
        -D USE_INTERNAL_DOUBLE_CONVERSION_LIBRARY:BOOL=False \
        -D USE_INTERNAL_RE2_LIBRARY:BOOL=False \
        -D USE_INTERNAL_GPERFTOOLS_LIBRARY:BOOL=False \
        -D ENABLE_TESTS:BOOL=False \
        -D UNBUNDLED:BOOL=False \
        .
  make clickhouse
}

package() {
  cd "ClickHouse/"
  mkdir -p $pkgdir/etc/clickhouse-server/ $pkgdir/etc/clickhouse-client/
  mkdir -p $pkgdir/usr/bin/
  mkdir -p $pkgdir/usr/lib/systemd/system
  ln -s clickhouse-client $pkgdir/usr/bin/clickhouse-server
  cp dbms/src/Server/config.xml dbms/src/Server/users.xml $pkgdir/etc/clickhouse-server/
  cp dbms/src/Server/clickhouse $pkgdir/usr/bin/clickhouse-client
  cp dbms/src/Server/clickhouse-client.xml $pkgdir/etc/clickhouse-client/config.xml

  for file in $(find "dbms/" -iname 'libclickhouse.so*');
  do
    cp -P "$file" "$pkgdir/usr/lib/$(basename $file)"
  done

  sed -e 's:/opt/clickhouse:/var/lib/clickhouse:g' -i $pkgdir/etc/clickhouse-server/config.xml
  sed -e '/listen_host/s%::<%::1<%' -i $pkgdir/etc/clickhouse-server/config.xml
  cp $startdir/clickhouse-server.service $pkgdir/usr/lib/systemd/system
}

# vim:set ts=2 sw=2 et:
