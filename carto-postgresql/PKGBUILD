# Maintainer: Javier Torres <jtorres@carto.com>
_pkgname=carto-postgresql
pkgname=$_pkgname-git
pkgver=0.19.2.r8.75df3b0
pkgrel=1
pkgdesc="PostgreSQL extension for CARTO"
arch=('x86_64')
url="https://github.com/CartoDB/cartodb-postgresql"
license=('BSD')
depends=('postgresql')
makedepends=('git')
conflicts=("$_pkgname")
provides=("${_pkgname}=${pkgver}")
_reponame=cartodb-postgresql
source=("git://github.com/Algunenano/$_reponame.git#branch=parallel")
sha256sums=('SKIP')

pkgver() {
	cd "$_reponame"
	printf "%s" "$(git describe --long | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

build() {
	cd "$_reponame"
	make all
}

package() {
	cd "$_reponame"
	DESTDIR=$pkgdir make install
	install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
