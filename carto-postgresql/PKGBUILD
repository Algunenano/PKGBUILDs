# Maintainer: Javier Torres <jtorres@carto.com>
_pkgname=carto-postgresql
pkgname=$_pkgname-git
pkgver=master.0.33.0.r15.4c15faf
pkgrel=1
pkgdesc="PostgreSQL extension for CARTO"
arch=('x86_64')
url="https://github.com/CartoDB/cartodb-postgresql"
license=('BSD')
depends=('postgresql' 'python-redis')
makedepends=('git')
conflicts=("$_pkgname")
provides=("${_pkgname}=${pkgver}")
_reponame=cartodb-postgresql
_owner="CartoDB"
_branch="master"
source=("git://github.com/$_owner/$_reponame.git#branch=$_branch")
sha256sums=('SKIP')

pkgver() {
	cd "$_reponame"
	printf "%s.%s" "$(echo $_branch | sed 's/-/_/g')" "$(git describe --long --tags | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

build() {
	cd "$_reponame"
	make all
}

package() {
	cd "$_reponame"
	DESTDIR=$pkgdir make install
	install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
